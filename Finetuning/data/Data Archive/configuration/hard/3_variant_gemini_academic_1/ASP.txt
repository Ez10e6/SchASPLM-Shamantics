% CPUs = {FalconX, CoreZen, EcoMini}.
cpu("FalconX"; "CoreZen"; "EcoMini").

% Costs: [FalconX: 600, CoreZen: 500, EcoMini: 300].
cost("FalconX", 600; "CoreZen", 500; "EcoMini", 300).

% Performance: [FalconX: 125, CoreZen: 115, EcoMini: 90].
perf("FalconX", 125; "CoreZen", 115; "EcoMini", 90).

% GPUs = {TitanZ, MidlineGT, Pixie}.
gpu("TitanZ"; "MidlineGT"; "Pixie").

% Costs: [TitanZ: 700, MidlineGT: 500, Pixie: 400].
cost("TitanZ", 700; "MidlineGT", 500; "Pixie", 400).

% Performance: [TitanZ: 135, MidlineGT: 100, Pixie: 95].
perf("TitanZ", 135; "MidlineGT", 100; "Pixie", 95).

% Drives = {FlashNova, Terabox, BudgetDisk}.
drive("FlashNova"; "Terabox"; "BudgetDisk").

% Costs: [FlashNova: 250, Terabox: 300, BudgetDisk: 150].
cost("FlashNova", 250; "Terabox", 300; "BudgetDisk", 150).

% Performance: [FlashNova: 110, Terabox: 100, BudgetDisk: 60].
perf("FlashNova", 110; "Terabox", 100; "BudgetDisk", 60).

% Select one CPU, one GPU, and one drive.
1 { selected_cpu(C) : cpu(C) } 1.
1 { selected_gpu(G) : gpu(G) } 1.
1 { selected_drive(D) : drive(D) } 1.

% Mid-performance component must be the cheapest.
selected(X) :- selected_cpu(X).
selected(X) :- selected_gpu(X).
selected(X) :- selected_drive(X).

max_perf(P) :- P = #max {V : perf(X, V), selected(X)}.
min_perf(P) :- P = #min {V : perf(X, V), selected(X)}.
mid_component(X) :- selected(X), perf(X, P), not min_perf(P), not max_perf(P).

cheapest(X) :- cost(X, C), selected(X), C = #min {C1 : cost(X1, C1), selected(X1)}.
:- mid_component(X1), cheapest(X2), X1 != X2.

% If CPU > $500 can not use Pixie GPU.
:- selected_cpu(C), cost(C, VC), VC > 500, selected_gpu("Pixie").

% Max - min performance ≤ 50.
:- max_perf(H), min_perf(L),  H > L + 50.

% If drive is FlashNova, GPU performance ≥ 100.
:- selected_drive("FlashNova"), selected_gpu(G), perf(G, P), P < 100.

% Exactly two selected components must cost > $300.
:- N != 2, N = #count {X : selected(X), cost(X, V), V > 300}.

% GPU performance must be between CPU and drive.
:- mid_component(X), not gpu(X).